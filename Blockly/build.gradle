plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'

    id 'maven-publish'

    //Kotlin Serialization
    id 'org.jetbrains.kotlin.plugin.serialization'
}

android {
    buildFeatures {
        compose true
    }
    compileSdk 30
    buildToolsVersion "30.0.3"

    composeOptions {
        kotlinCompilerVersion kotlinCompilerVersion
        kotlinCompilerExtensionVersion compose_version
    }
    defaultConfig {
        minSdk 21
        targetSdk 30

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    //Compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"

    //Markdown Library
    implementation "io.noties.markwon:core:4.6.2"
    implementation 'io.noties.markwon:html:4.6.2'
    implementation 'io.noties.markwon:ext-strikethrough:4.6.2'
    implementation 'io.noties.markwon:ext-tasklist:4.6.2'

    //Kotlin Serialization
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.2'
}

def versionName = "0.2.3"
def artifactName = "blockly"

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties")))

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = "com.wakaztahir"
                artifactId = artifactName
                version = versionName
            }
        }
        repositories {
            maven {
                name = "GithubPackages"
                /** Configure path of your package repository on Github
                 *  Replace GITHUB_USERID with your/organisation Github userID and REPOSITORY with the repository name on GitHub
                 */
                url = uri("https://maven.pkg.github.com/timeline-notes/blockly")

                credentials {
                    /**Create github.properties in root project folder file with gpr.usr=GITHUB_USER_ID  & gpr.key=PERSONAL_ACCESS_TOKEN**/
                    username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                    password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
                }
            }
        }
    }
}